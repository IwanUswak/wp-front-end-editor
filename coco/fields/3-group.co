class FrontEndEditor.fieldTypes.group extends FrontEndEditor.fieldTypes.input
	($container, editors) ->
		@el = $container
		@editors = editors

		@has_aloha = false

		if GENTICS?.Aloha
			for editor of @editors
				if 'rich' == editor.get_type()
					@has_aloha = true
					return

	start_editing: (ev) ->
		@create_form()

		if @has_aloha
			GENTICS.Aloha.wpSaveCancel.group = this
		else
			@create_buttons()

		@ajax_get()

		false

	create_form: ->
		for editor of @editors
			editor.create_form()
			editor.create_input()

		super ...

		@el.append @form

	remove_form: (ev) ->
		for editor of @editors
			editor.remove_form()

		super ...

		if @has_aloha
			GENTICS.Aloha.wpSaveCancel.group = void

	content_from_input: ->
		editor.content_from_input() for editor of @editors

	keypress: jQuery.noop

	ajax_set: ->
		super ...

		FrontEndEditor.overlay.cover @el

	ajax_args: ->
		args = super ...

		args <<<
			group : true,
			data  : (editor.data for editor of @editors)

		args

	ajax_get_handler: (response) ->
		for editor, i of @editors
			editor.ajax_get_handler response[i]

		@editors[0].input?.focus()

	ajax_set_handler: (response) ->
		for editor, i of @editors
			editor.ajax_set_handler response[i]

		@remove_form()
